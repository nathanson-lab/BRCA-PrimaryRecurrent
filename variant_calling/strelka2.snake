#############################
#Brad Wubbenhorst
#bwubb@pennmedicine.upenn.edu
#Mar. 2021

#This code is used to run Strelka2 paired somatic variant calling.

import os

### INIT ###

#List of samples
with open(config.get('project',{}).get('sample_list','samples.list'),'r') as i:
    SAMPLES=i.read().splitlines()
    for sample in SAMPLES:
        os.makedirs(f'logs/cluster/{sample}',exist_ok=True)

#Tab separated file. Tumor Normal pairs
with open(config.get('project',{}).get('pairs_table','pairs.table'),'r') as p:
    PAIRS=dict(line.split('\t') for line in p.read().splitlines())

#Tab separated file. Sample /path/to/file.bam
with open(config.get('project',{}).get('bams_table','bams.table'),'r') as b:
    BAMS=dict(line.split('\t') for line in b.read().splitlines())

### FUNCTIONS ###

def paired_bams(wildcards):
    ref=config['reference']['key']
    tumor=wildcards.tumor
    normal=PAIRS[wildcards.tumor]
    return {'tumor':BAMS[wildcards.tumor],'normal':BAMS[normal]}

### SNAKEMAKE ###

wildcard_constraints:
    work_dir=f"data/work/{config['resources']['targets_key']}"

rule collect_strelka2:
    input:
        expand("data/work/{lib}/{tumor}/strelka2/results/variants/somatic.raw.norm.clean.std.vcf.gz",lib=config['resources']['targets_key'],tumor=PAIRS.keys())

rule write_Manta:
    input:
        unpack(paired_bams)
    output:
        temp("{work_dir}/{tumor}/manta/runWorkflow.py")
    params:
        runDir="{work_dir}/{tumor}/manta",
        reference=config['reference']['fasta'],
        bedgz=config['resources']['targets_bedgz']
    shell:
        "$HOME/software/manta/bin/configManta.py --normalBam {input.normal} --tumorBam {input.tumor} --referenceFasta {params.reference} --callRegions {params.bedgz} --exome --runDir {params.runDir}"

rule run_Manta:
    input:
        "{work_dir}/{tumor}/manta/runWorkflow.py"
    output:
        "{work_dir}/{tumor}/manta/results/variants/candidateSmallIndels.vcf.gz",
        "{work_dir}/{tumor}/manta/results/variants/candidateSV.vcf.gz",
        "{work_dir}/{tumor}/manta/results/variants/diploidSV.vcf.gz",
        "{work_dir}/{tumor}/manta/results/variants/somaticSV.vcf.gz"
    threads:
        4
    shell:
        "{input} -m local -j {threads}"

rule write_Strelka2:
    input:
        unpack(paired_bams),
        indels="{work_dir}/{tumor}/manta/results/variants/candidateSmallIndels.vcf.gz"
    output:
        temp("{work_dir}/{tumor}/strelka2/runWorkflow.py")
    params:
        runDir="{work_dir}/{tumor}/strelka2",
        reference=config['reference']['fasta'],
        bedgz=config['resources']['targets_bedgz']
    shell:
        "$HOME/software/strelka/bin/configureStrelkaSomaticWorkflow.py --normalBam {input.normal} --tumorBam {input.tumor} --indelCandidates {input.indels} --referenceFasta {params.reference} --callRegions {params.bedgz} --exome --runDir {params.runDir}"

rule run_Strelka2:
    input:
        "{work_dir}/{tumor}/strelka2/runWorkflow.py"
    output:
        snvs="{work_dir}/{tumor}/strelka2/results/variants/somatic.snvs.vcf.gz",#not snps
        indels="{work_dir}/{tumor}/strelka2/results/variants/somatic.indels.vcf.gz",
        raw="{work_dir}/{tumor}/strelka2/results/variants/somatic.raw.vcf.gz"
    params:
        workdir="{work_dir}/{tumor}/strelka2",
        reference=config['reference']['fasta'],
        bedgz=config['resources']['targets_bedgz']
    threads:
        4
    shell:
        """
        {input} -m local -j {threads}
        bcftools concat -a --threads {threads} {output.snvs} {output.indels} | bcftools sort -O z -o {output.raw}
        tabix -f -p vcf {output.raw}
        """
        #can add -m, --max-mem <float>[kMG]    maximum memory to use [768M] for sort

rule Strelka2_somatic_normalized:
    input:
        "{work_dir}/{tumor}/strelka2/results/variants/somatic.raw.vcf.gz"
    output:
        norm="{work_dir}/{tumor}/strelka2/results/variants/somatic.raw.norm.vcf.gz",
        clean="{work_dir}/{tumor}/strelka2/results/variants/somatic.raw.norm.clean.vcf.gz"
    params:
        regions=config['resources']['targets_bedgz'],
        ref=config['reference']['fasta']
    shell:
        """
        bcftools norm -m-both {input} | bcftools norm -f {params.ref} -O z -o {output.norm}
        tabix -f -p vcf {output.norm}
        bcftools view -e 'ALT~\"*\"' -R {params.regions} {output.norm} | bcftools sort -O z -o {output.clean}
        tabix -f -p vcf {output.clean}
        """

rule Strelka2_somatic_standardized:
    input:
        "{work_dir}/{tumor}/strelka2/results/variants/somatic.raw.norm.clean.vcf.gz"
    output:
        "{work_dir}/{tumor}/strelka2/results/variants/somatic.raw.norm.clean.std.vcf.gz"
    params:
        tumor=lambda wildcards: wildcards.tumor,
        normal=lambda wildcards: PAIRS[wildcards.tumor],
        lib=config['resources']['targets_key'],
        mode='strelka2'
    shell:
        "python standardize_vcf.py -i {input} -T {params.tumor} -N {params.normal} -m {params.mode}"